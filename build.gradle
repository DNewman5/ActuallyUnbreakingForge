buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {

            url = 'https://dist.creeper.host/Sponge/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = "1.12.2-0.0.1"
group = "wutdahack.actuallyunbreaking"
archivesBaseName = "actuallyunbreaking"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        ext.refMap = 'mixins.actuallyunbreaking.refmap.json'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', "1.12.2-14.23.4.2705"
    inputs.property 'mcversion', "snapshot_20180814"

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = 'run'

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20180814"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    def args = [
            '-Dfml.coreMods.load=wutdahack.actuallyunbreaking.core.AUCoreMod',
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true'
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

repositories {
    //mavenCentral()
    maven {
        url = 'https://dist.creeper.host/Sponge/maven'
    }
}

dependencies {
    embed('org.spongepowered:mixin:0.8') {
        transitive = false
    }
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }

    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.actuallyunbreaking.json',
            'FMLCorePlugin': 'wutdahack.actuallyunbreaking.core.AUCoreMod'
    )
}